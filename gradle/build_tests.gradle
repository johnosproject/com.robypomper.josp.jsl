/*******************************************************************************
 * The John Operating System Project is the collection of software and configurations
 * to generate IoT EcoSystem, like the John Operating System Platform one.
 * Copyright (C) 2021 Roberto Pompermaier
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 ******************************************************************************/

/**
 * Gradle configurations to manage JOSP project's tests.
 *
 * Summary:
 * - SourceSes configs
 * - Tests Tasks
 * - IntelliJ Modules
 *
 * This file configure and provide test tasks for all JOSP project's components:
 * - JOSP Commons: communication tests
 * - JOD: communication and structure tests
 * - JSL: communication tests
 *
 * Component's test are organized by one sourceSets for each unit of the component
 * (comm, struct, perms...) and this file provide specific and generic (All) tests
 * tasks.
 */


// -----------------
// SourceSes configs
// -----------------

sourceSets {

    // Test's Commons

    test_Commons {
        java.srcDirs 'tests/test_Commons/java'
        resources.srcDirs 'tests/test_Commons/resources'
    }


    // JSL

    test_jospJSLComm {
        java.srcDirs 'tests/test_jospJSLComm/java'
        resources.srcDirs 'tests/test_jospJSLComm/resources'
    }

}

dependencies {
    // Test's commons
    test_CommonsImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    test_CommonsRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
    test_CommonsImplementation "com.robypomper.josp:jospJOD:${project.ext.get("version.deps.jospCommons")}"
    test_CommonsImplementation sourceSets.jospJSL.output
    test_CommonsImplementation sourceSets.jospJSL.runtimeClasspath

    // JSL
    test_jospJSLCommImplementation sourceSets.test_Commons.output
    test_jospJSLCommImplementation sourceSets.test_Commons.runtimeClasspath
}


// -----------
// Tests Tasks
// -----------

task jospJSLAllTest(type: Test) {
    description = 'Runs all JSL tests.'
    group = 'verification'
}
check.dependsOn jospJSLAllTest

task jospJSLCommTest(type: Test) {
    description = 'Runs JSL Communication tests.'
    group = 'verification'

    testClassesDirs = sourceSets.test_jospJSLComm.output.classesDirs
    classpath = sourceSets.test_jospJSLComm.runtimeClasspath
    shouldRunAfter test
    useJUnitPlatform()
    //jvmArgs '-Djavax.net.debug=all'
}
jospJSLAllTest.dependsOn jospJSLCommTest


// ----------------
// IntelliJ Modules
// ----------------

// gradle > intellij settings
idea {
    module {
        testSourceDirs += project.sourceSets.test_jospJSLComm.java.srcDirs
        testSourceDirs += project.sourceSets.test_jospJSLComm.resources.srcDirs
    }
}
